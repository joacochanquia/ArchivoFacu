PIC	EQU	20H
EOI	EQU	20H
N_F10	EQU	10
PIO 	EQU 	30H
; VECTOR DE INTERRUPCIONES
	ORG	40
IP_F10	DW	RUT_F10
; DATOS
	ORG 	1000H
NUM_CAR DB 	5
CAR 	DB 	?
; SUBRUTINA DE INICIALIZACION 
; PIO PARA IMPRESORA 
	ORG 	3000H 
INI_IMP:MOV 	AL, 0FDH 
	OUT 	PIO+2, AL 
	MOV 	AL, 0 
	OUT 	PIO+3, AL 
	IN 	AL, PIO 
	AND 	AL, 0FDH 
	OUT 	PIO, AL 
	RET
; SUBRUTINA DE GENERACIÃ“N
; DE PULSO 'STROBE'
	ORG	4000H
PULSO:	PUSH	AX
	IN 	AL, PIO
	OR 	AL, 02H
	OUT 	PIO, AL
	IN 	AL, PIO
	AND 	AL, 0FDH
	OUT 	PIO, AL
	POP	AX
	RET
; RUTINA INTERRUPCION F10
	ORG	5000H
RUT_F10:PUSH	AX
POLL: 	IN 	AL, PIO
	AND 	AL, 1
	JNZ 	POLL
	MOV 	AL, [BX]
	OUT 	PIO+1, AL
	CALL 	PULSO
	MOV	AL, EOI
	OUT	PIC, AL
	MOV	DH, 0FFH
	POP	AX
	IRET
; PROGRAMA PRINCIPAL
	ORG 	2000H
	CLI
	MOV	AL, 0FEH
	OUT	PIC+1, AL
	MOV	AL, N_F10
	OUT	PIC+4, AL
	PUSH 	AX
	CALL 	INI_IMP
	POP 	AX
	MOV 	BX, OFFSET CAR
	MOV 	CL, NUM_CAR
	MOV	DH, 00H
OTRO: 	STI
	INT 	6
LAZO:	CMP	DH, 00H
	JZ	LAZO
	CLI
	MOV	DH, 00H
	DEC 	CL
	JNZ 	OTRO
	STI
	INT 	0
	END
